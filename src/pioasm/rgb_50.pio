; RGB for VGA output
; by gregg, based on Hunter Adams' library

.program rgb_50

; This program just sends pixels to VGA. This version handles 720x576p50 output mode in CEA-861 standard. 
; But there is one extra trick added - pixels are sent a bit faster, so in the time that we should send 720 pixels,
; we actually send 800, so this way we can show the whole Amstrad screen (at least horizontally) including overscan.
; The timings in other state machines follow the timing of 720x576p50 mode so monitor will still detect it as 720x576p50.
;
; We don't set clock divider here, so this state machine runs at full speed - 128Mhz. Pixel clock should be 27Mhz.
; We want to scale it up by 800/720 = 1.11111 , so the ideal pixel frequency would be 30Mhz.
; 4 cycles per pixel will give us 32Mhz that is close enough - slightly faster so we will not fill exactly the whole screen.

pull block 					; Pull number of active pixels per line - 1
mov y, osr 					; And keep value in register y

.wrap_target            ; One iteration per line

set pins, 0 			; Zero RGB pins during blank period (hsync + porches)
mov x, y                ; x will be used to count cycles

wait 1 irq 1            ; When we get this irq, it means backporch is finished and we should send pixels

onepixel:                   ; 4 cycles per pixel total:
	pull block				  ; pull color value  (1 cycle)
    out pins, 6 [1] 		  ; set output rgb pins (2 cycles)
	jmp x-- onepixel		  ; repeat for each pixel in line (1 cycle)

.wrap


% c-sdk {
static inline void rgb_50_program_init(PIO pio, uint sm, uint offset, uint pin) {
    pio_sm_config c = rgb_50_program_get_default_config(offset);
    sm_config_set_set_pins(&c, pin, 6);   // 2 pins per color, 6 in total
    sm_config_set_out_pins(&c, pin, 6);
    for (int i = 0; i < 6; ++i) {
        pio_gpio_init(pio, pin + i);
    }    
    pio_sm_set_consecutive_pindirs(pio, sm, pin, 6, true);
    pio_sm_init(pio, sm, offset, &c);
}
%}
